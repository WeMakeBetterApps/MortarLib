apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

configurations { providedCompile }

sourceSets.main.compileClasspath += configurations.providedCompile
sourceSets.test.compileClasspath += configurations.providedCompile
sourceSets.test.runtimeClasspath += configurations.providedCompile

version = '1.2.1'

dependencies {
    providedCompile 'com.squareup.flow:flow:0.7'
    providedCompile 'com.squareup.mortar:mortar:0.16'
    providedCompile 'com.google.code.gson:gson:2.5'
    providedCompile 'com.jakewharton:butterknife:7.0.1'
    providedCompile 'com.google.android:android:4.0.1.2'
    providedCompile files('libs/android-support-v4.jar')
    providedCompile files('libs/android-support-v7-appcompat.jar')
}

task sourcesJar(type: Jar, dependsOn:classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

archivesBaseName = "mortar-lib"

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'MortarLib'
                packaging 'jar'
                // optionally artifactId can be defined here
                artifactId = 'mortar-lib'
                description 'Supports building Android apps using Square\'s Flow and Mortar.'
                url 'https://github.com/WeMakeBetterApps/MortarLib'

                scm {
                    connection 'scm:git:http://github.com/wemakebetterapps/mortarlib'
                    developerConnection 'scm:git:https://github.com/wemakebetterapps/mortarlib'
                    url 'http://github.com/wemakebetterapps/mortarlib'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'spierce7'
                        name 'Scott Pierce'
                        email 'scott@wemakebetterapps.com'
                    }
                }
            }
        }
    }
}